timer += timer_speed;
if ( timer_speed == 0 ) exit;

moving_object = 1;

var o;
o = instance_nearest ( x , y , player );
if ( instance_exists ( o ) )
  {
  if ( o.x < x - 16 )
    facing = 1;
  if ( o.x > x + 16 )
    facing = 0;
  }

switch ( mode )
  {
  // spawn
  case 0:
    sprite_index = spr[1];
    image_index = floor ( timer * 0.5 ) mod 4;
    vspeed += 0.25;
    shakey = lengthdir_y ( 32 , timer / time * 180 );
    if ( timer >= time )
      {
      sprite_index = spr[0];
      timer = 0;
      mode = 1;
      time = 10;
      }
    break;
  // land
  case 1:
    squish = 1 + lengthdir_y ( 0.2 , timer / time * 180 );
    if ( timer >= time )
      {
      squish = 1;
      mode = 2;
      timer = 0;
      time = 180;
      sprite_index = spr[2];
      image_index = 0;
      }
    break;
  // idle
  case 2:
    image_index = ( image_index + 0.25 ) mod sprite_get_number ( sprite_index );
    if ( timer >= time )
      {
      sound_play2 ( sfx[0] );
      mode = 3;
      timer = 0;
      time = 250;
      sprite_index = spr[3]; 
      image_index = 0;
      }
    break;
  // taunt
  case 3:
    switch ( timer )
      {
      case 4: image_index = 1; break;
      case 8: image_index = 2; break;
      case 12: image_index = 3; break;
      case 16: image_index = 4; break;
      case 125: case 145: case 165: case 185: case 205: case 225: image_index = 5; break;
      case 135: case 155: case 175: case 195: case 215: image_index = 4; break;
      case 230: image_index = 6; break;
      case 240: image_index = 7; break;
      }
    if ( timer >= time )
      {
      sprite_index = spr[0];
      timer = 0;
      time = 10;
      mode = 4;
      }
    break;
  // jumpsquat
  case 4:
    squish = 1 + lengthdir_y ( 0.25 , timer / time * 90 );
    if ( timer >= time )
      {
      sprite_index = spr[1]; 
      squish = 1;
      mode = 5;
      timer = 0;
      time = 25;
      image_index = 0;
      vspeed = -3;
      }
    break;
  // jump out
  case 5:
    vspeed += 0.25;
    image_index = ( image_index + 1 ) mod 4;
    if ( vspeed >= 0 )
      {
      instance_destroy();
      }
    break;
  }