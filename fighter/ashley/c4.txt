// final smash pot/flower/vine

attacker.smash_power = 0;

if ( mode == 2 )
  {
  if ( timer == 10 ) hit = 0;
  if ( timer < 10 )
    {
    image_yscale += 0.1;
    }
    else
    {
    if ( timer < time - 20 )
      {
      if ( image_yscale > 0.8 )
        image_yscale -= 0.025;
      }
      else
      {
      image_yscale -= 0.1;
      if ( image_yscale <= 0 ) instance_destroy();
      }
    }
  time = 300;
  //if ( !on_floor && !on_platform )
  //  instance_destroy();
  exit;
  }

if ( mode )
  {
  time = 600;
  if ( !on_floor && !on_platform )
    vspeed += 0.25;
    else
    vspeed = 0;
  if ( timer mod 4 == 0 && image_index < 6 )
    screen_shake ( 3 , 5 );
  if ( timer == 4 )
    sound_play_pitch ( explode1 , 0.4 );
  if ( timer > 10 && timer mod 40 == 0 && timer < time - 60 )
    {
    var a, xx;
    xx = -999;
    repeat ( 8 )
      {
      a = -192 + random ( 384 );
      if ( collision_line ( x + a , y - 48 , x + a , y + 48 , obj_platform , 1 , 1 )
        || collision_line ( x + a , y - 48 , x + a , y + 48 , obj_floor , 1 , 1 ) )
        {
        xx = a;
        break;
        }
      }
    if ( xx != -999 )
      {
      sound_play_pitch ( explode_small , 0.4 + random ( 0.2 ) );
      create_projectile ( 8 , 90 , 8 , 8 , -1 , 8 , slash3 , obj_spark_slash3 , 1 , 1 , 0 , 0 , attacker.id , 90 , char_custom4 );
      proj.x = x + xx;
      proj.y = y - 49;
      with ( proj )
        {
        repeat ( 100 )
          {
          if ( !collision_point ( x , y + 1 , obj_platform , 1 , 1 ) && !collision_point ( x ,y + 1 , obj_floor , 1 , 1 ) )
            y += 1;
            else
            break;
          }
        }
      proj.mode = 2;
      proj.image_yscale = 0.1;
      proj.image_xscale = 0.8;
      proj.hit = 2;
      proj.sprite_index = attacker.final_smash_vines;
      //proj.sprite_index = spr_collision_bubble_alt;
      proj.image_speed = 0.5;
      proj.alarm[0] = 90;
      }
    }
  if ( timer < time - 60 )
    {
    image_index += 0.125;
    if ( image_index >= 13.5 ) image_index = 6;
    if ( image_index >= 6 && timer mod 10 == 0 )
      hit = 0;
    }
    else
    {
    if ( image_index < 14 )
      image_index = 14;
      else
      image_index += 0.25;
    if ( image_index >= 21.5 ) instance_destroy();
    }
  exit;
  }

moving_object = 1;
vspeed += 0.25;

if ( image_index >= image_number - 1 ) image_index = image_number - 1;

if ( on_floor || on_platform )
  {
  hspeed = 0;
  vspeed = 0;
attacker.look=1
  sound_play ( attacker.fs_sound );
  spark = instance_create ( x , y , obj_spark_shockwave );
  spark.image_xscale *= 2;
  spark.image_yscale *= 0.5;
  instance_create ( x , y , obj_spark_hit1 );
  timer = 0;
  mode = 1;
  alarm[0] = 600;
  time = 600;
  sprite_index = attacker.final_smash_plant;
  image_speed = 0;
  image_index = 0;
  }