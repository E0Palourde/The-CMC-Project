if(time == -1)
{
	hspeed = 0;
	state_type = "attack";
	attack_type = "dthrow";
	image_speed = 0.6;
	sprite_index = dthrow_sprite;
	image_index = 0;
	timer = 0;
	time = 28;
	timer_speed = 1;
	target.timer_speed = 0;
	target.alarm[0] = 50;
	target.state_type = "air_flinch";
	target.depth = depth - 1;
}

if(timer < time)
{
	if(timer < 4)	//frames 1-5
	{
		target.x = (x + target.x) * 0.5;
		target.y = (y - 9 + target.y) * 0.5;
	}
	if(timer == 4)	//frame 5
	{
		target.x = x - 2 + facing * 4;
		target.y = y - 9;
	}
	if(timer >= 5 && timer < 27)	//frames 6-27
	{
		shakey = lengthdir_y(max(2 , target.actual_height - actual_height * 0.5), (timer - 5) / 22 * 180);
	}
	else
	{
		shakey = 0;
	}
	
	if(timer >= 4 && timer < 10)	//frames 5-10
	{
		target.x = x - 2 + facing * 4;
		target.y -= 0.25;
	}
	
	if(timer >= 11 && timer <= 15)	//frames 12-16
	{
		target.x = x - 2 + facing * 4;
		target.y += 2
	}
	
	if(timer == 14)	//frame 15
	{
		sound_play(attacksnd2);
		
		/* choose() probability table by HFF
         * 
         * Parameter    | # appearances | probability (%)
         * attack_voice2| 1             | 33.3
         * attack_voice1| 1             | 33.3
         * attack_voice3| 1             | 33.3
         */
		sound_play(choose(attack_voice1, attack_voice2, attack_voice3));
		create_hitbox(
        /* Damage            */ 2,
        /* Direction         */ 270,
        /* Base KB           */ 1,
        /* KB Growth         */ 1,
        /* Shield Damage     */ 1,
        /* Hitlag            */ 3,
        /* SFX               */ kick2,
        /* VFX               */ -1,
        /* Width             */ 0.8,
        /* Height            */ 0.8,
        /* X                 */ 0,
        /* Y                 */ 9,
        /* Owner             */ self.id,
        /* Active Frames     */ 1,
        /* Priority          */ 5
        );
		instance_create(x, y, obj_down_impact);
		hitbox.di = -1;
		target.gethit = false;
	}
	
	if(timer == 15)	//frame 16
	{
		script_execute(create_throw_box,
		/* Damage            */ 6,
        /* Direction         */ 80 + facing * 20,
        /* Base KB           */ 5.75,
        /* KB Growth         */ 2.7,
        /* Shield Damage     */ 1,
        /* Hitlag            */ 1,
        /* SFX               */ -1,
        /* VFX               */ obj_spark_effect,
        /* Width             */ 0.8,
        /* Height            */ 0.8,
        /* X                 */ 0,
        /* Y                 */ 9,
        /* Owner             */ self.id,
        /* Active Frames     */ 1
        );
		hitbox.hit_stun = 6 + (target.percent * 0.1);
	}
}
		
if(timer >= 27 && state_type != "flinch" && state_type != "air_flinch")
{
    attack_type = "none";
    state_type = "stand";
    timer = 0;
    time = -1;
}
//S/O to HFF